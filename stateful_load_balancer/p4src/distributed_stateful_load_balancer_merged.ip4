/* Copyright 2018-present IIT Bombay (Akash Trehan, Huzefa Chasmai, Aniket Shirke)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// ------------------ Constants --------------------

#define SERVERS 2
#define SERVERMINUSONE 1
#define SERVERPLUSONE 3
#define SWITCHES 12
#define SWITCHMINUSONE 11
#define SWITCHPLUSONE 13
#define SWITCHPLUSSERVER 14
#define MCAST_GRP 1
#define THRESHOLD 4
#define SQTHRESH 25
#define UPPER_LIMIT 70
#define LOWER_LIMIT 30

// -------------------- Headers ------------------------

header_type load_balancer_t {
    fields {
        preamble: 64;
        syn: 32;
        fin: 32;
        fid: 32;
        subfid : 32;
        packet_id : 32;
        hash : 32;
        _count : 32;
    }
}

header load_balancer_t load_balancer_head;

header_type meta_t {
    fields {
        temp : 32;

        @pcube_for (i) (1,SERVERS,1)
            server_flow$i : 32;
        @pcube_endfor

        @pcube_for (i) (1,SWITCHPLUSONE,1)
            switch_flow$i : 32;
        @pcube_endfor

        hash: 16;
        routing_port: 32;

        probe_bool : 32;

        upper_limit : 32;
        lower_limit : 32;
    }
}
metadata meta_t meta;

// ---------------------- Hashing ------------------

field_list flow_list {
    load_balancer_head.fid;
}

field_list_calculation flow_register_index {
    input {
        flow_list;
    }
    algorithm : crc16;
    output_width : 16;
}

//-------------------- Parsers ------------------------

parser start {
    return select(current(0, 64)) {
        0: parse_head;
        1: parse_head;
        2: parse_head;
        default: ingress;
    }
}

parser parse_head {
    extract(load_balancer_head);
    extract(sync_info);
    return ingress;
}

//------------------ Registers -----------------------

register total_flow_count_register {
    width: 32;
    instance_count: SWITCHPLUSSERVER;
}

register flow_to_port_map_register {
    width: 32;
    instance_count: 65536;
}

// --------------------- Tables -------------------------------

table get_limits_table{
    actions{
        get_limits;
    }
    size:1;
}

table get_server_flow_count_table{
    actions{
        get_server_flow_count;
    }
    size:1;
}

table update_switch_flow_count_table {
    actions{
        update_switch_flow_count;
    }
    size: 1;
}

table set_server_dest_port_table{
    reads{
        @pcube_for (i) (1,SERVERS,1)
            meta.server_flow$i : exact;
        @pcube_endfor
    }
    actions{
        set_server_dest_port;
    }
    size:SQTHRESH;
}

table set_probe_bool_table {
    actions{
        set_probe_bool;
    }
    size: 1;
}

table get_switch_flow_count_table{
    actions{
        get_switch_flow_count;
    }
    size:1;
}

@pcube_for (i) (1,SWITCHPLUSONE,1)
    table set_switch$i_dest_port_table{
        actions{
            set_switch$i_dest_port;
        }
        size:1;
    }
@pcube_endfor

table update_map_table {
    actions {
        update_map;
    }
    size: 1;
}

table forwarding_table {
    reads{
        meta.routing_port: valid;
    }
    actions{
        forward;
        _drop;
    }
    size: 1;
}

table drop_table {
    actions {
        _drop;
    }
    size: 1;
}

table clear_map_table {
    actions {
        clear_map;
    }
    size: 1;
}

table update_flow_count_table {
    actions{
        update_flow_count;
    }
    size: 1;
}

// ---------------------------- Actions -----------------------

action get_limits(upper_limit, lower_limit){
    modify_field(meta.upper_limit, upper_limit);
    modify_field(meta.lower_limit, lower_limit);
}

action get_server_flow_count(){

    @pcube_for (i) (1,SERVERS,1)
        register_read(meta.server_flow$i, total_flow_count_register, $i - 1);
    @pcube_endfor
}

action update_switch_flow_count() {
    register_write(total_flow_count_register, standard_metadata.ingress_port - 2, sync_info._0);
}

action set_server_dest_port(flow_count,flow_dest){
    register_write(total_flow_count_register, flow_dest - 2, flow_count + 1);
    modify_field(standard_metadata.egress_spec, flow_dest);
}

action set_probe_bool(){
    modify_field(meta.probe_bool, 1);
}

action get_switch_flow_count(){
    @pcube_for (i) (1,SWITCHPLUSONE,1)
        register_read(meta.switch_flow$i, total_flow_count_register, $i + SERVERS - 2);
    @pcube_endfor
}

@pcube_for (i) (1,SWITCHPLUSONE,1)
    action set_switch$i_dest_port(){
        register_write(total_flow_count_register, $i + SERVERS - 2, meta.switch_flow$i + 1);
        modify_field(standard_metadata.egress_spec, $i + SERVERS);
    }
@pcube_endfor

action update_map() {
    modify_field_with_hash_based_offset(meta.hash, 0, flow_register_index, 65536);
    register_write(flow_to_port_map_register, meta.hash, standard_metadata.egress_spec);
}

action forward() {
    modify_field_with_hash_based_offset(meta.hash, 0, flow_register_index, 65536);
    register_read(meta.routing_port, flow_to_port_map_register, meta.hash);
    modify_field(standard_metadata.egress_spec, meta.routing_port);
}

action clear_map() {
    modify_field_with_hash_based_offset(meta.hash, 0, flow_register_index, 65536);
    register_write(flow_to_port_map_register, meta.hash, 0);
}

action update_flow_count() {
    register_read(meta.temp, total_flow_count_register, meta.routing_port-2);
    add_to_field(meta.temp, -1);
    register_write(total_flow_count_register, meta.routing_port-2, meta.temp);
}

action _drop() {
    drop();
}

//------------------------ Control Logic -----------------

control ingress {

    //Get threshold from table instead of hardcoding
    apply(get_limits_table);  

    //Get server flow counts from the table  
    apply(get_server_flow_count_table); 

    //Preamble 1 => Probe packet 
    if (load_balancer_head.preamble == 1){

        //Send update (set preamble as 2)
        @pcube_echo(load_balancer_head.preamble,2)
            @pcube_sum (1, SERVERS, 1) (meta.server_flow$i)
        @pcube_endecho
    }

    //Preamble 2 => Update packet
    else if (load_balancer_head.preamble == 2){
        //update the registers
        apply(update_switch_flow_count_table);
    }

    //Default Preamble is 0
    else {

        //-------------------------------------------------------------------------
        //Start of flow
        if(load_balancer_head.syn == 1) {

            //forwarding to own server
            if(@pcube_cmp (1,SERVERS,1)(<)(or)(meta.server_flow$i, THRESHOLD)){
                
                //forwarding can be done locally
                apply(set_server_dest_port_table);

                //Take decision to send probe packet or not (Reactive)
                if ((@pcube_sum (1, SERVERS,1)(meta.server_flow$i))*100 > (meta.upper_limit * SERVERMINUSONE * THRESHOLD)){
                    apply(set_probe_bool_table);
                }
            }
            
            //forwarding to another switch
            else{
                //Choose from switches
                apply(get_switch_flow_count_table);

                //all switches reach threshold
                if (@pcube_cmp (1,SWITCHPLUSONE,1)(>=)(and)(meta.switch_flow$i, 2*THRESHOLD)){
                    apply(drop_table);
                }

                //choose the switch handling least number of flows
                else {
                    @pcube_minmax (<=)
                    @pcube_for (i) (1,SWITCHPLUSONE,1)
                        @pcube_case meta.switch_flow$i:
                            apply(set_switch$i_dest_port_table);
                        @pcube_endcase
                    @pcube_endfor
                    @pcube_endminmax
                }
                
            }

            //Remember mapping for the flow
            apply(update_map_table);
        }
        //-------------------------------------------------------------------------

        //forwarding
        apply(forwarding_table);

        if(meta.probe_bool == 1){
            @pcube_sync(load_balancer_head.preamble,1)
            @pcube_endsync
        }

        //-------------------------------------------------------------------------
        //End of flow
        if(load_balancer_head.fin == 1) {

            //Clear mappings
            apply(clear_map_table);
            apply(update_flow_count_table);

            //Take decision to send probe packet or not (Proactive)
            if ((@pcube_sum (1, SERVERS ,1)(meta.server_flow$i))*100 < (meta.lower_limit * SERVERMINUSONE * THRESHOLD)){
                if(meta.routing_port == 2 or meta.routing_port == 3){

                    //Send broadcast (set preamble as 2)
                    @pcube_sync(load_balancer_head.preamble,2)
                        @pcube_sum(1, SERVERS, 1)(meta.server_flow$i)
                    @pcube_endsync
                }
            }
        }
        //-------------------------------------------------------------------------
    }
}

control egress {
}
